apply plugin: 'com.android.application'

android {
    compileSdkVersion 30

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    defaultConfig {
        applicationId "com.pingidentity.pingone"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode = 11
        versionName "1.6.0"

        // Make sure this is consistent with the redirect URI used in res/raw/auth_config.json,
        // or specify additional redirect URIs in AndroidManifest.xml
        manifestPlaceholders = [
                'appAuthRedirectScheme': 'pingonesdk://sample'
        ]
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "OIDC_ISSUER", OIDC_ISSUER
        buildConfigField "String", "CLIENT_ID", CLIENT_ID
        buildConfigField "String", "SCOPE", SCOPE
        buildConfigField "String", "OIDC_REDIRECT_URI", OIDC_REDIRECT_URI
        /*
         * This is the variable that is injected into build config, while building the application.
         * If you want to use the Android Device Verification feature you should assign a value to
         * this variable in the gradle.properties file. Access this value from the application in the
         * following way:
         * String safetyNetApiKey = BuildConfig.API_KEY;
         */
        buildConfigField("String", "API_KEY", "${safetynet_api_key}")
    }
    lintOptions {
        abortOnError false
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

}
dependencies {
    implementation fileTree(dir: 'libs', include: ['*.aar'])
    implementation project(':PingAuthenticationUI')
    implementation 'org.slf4j:slf4j-api:1.7.30'
    implementation 'com.github.tony19:logback-android:2.0.0'

    implementation 'com.madgag.spongycastle:core:1.58.0.0'
    implementation 'com.madgag.spongycastle:bcpkix-jdk15on:1.58.0.0'

    //Import the Firebase BoM (Bill of Materials)
    implementation platform('com.google.firebase:firebase-bom:26.3.0')
    //FireCloud Messaging Services
    implementation 'com.google.firebase:firebase-core'
    implementation 'com.google.firebase:firebase-messaging'

    /*
     * The jose.4.j library is an open source (Apache 2.0) implementation of JWT and the JOSE
     * specification suite
     */
    implementation 'org.bitbucket.b_c:jose4j:0.7.1'

    //Google's gSon library to build and parse JSON format
    implementation 'com.google.code.gson:gson:2.8.6'

    //to manage lifecycle easily
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'


    /*
     * AppAuth for Android is a client SDK for communicating with OAuth 2.0 and OpenID Connect providers
     * for versions look at https://bintray.com/openid/net.openid/appauth#
     */
    implementation 'net.openid:appauth:0.7.1'

    testImplementation 'junit:junit:4.13.1'
}
apply plugin: 'com.google.gms.google-services'

apply plugin: 'com.android.application'
apply plugin: 'androidx.navigation.safeargs'

android {
    compileSdkVersion 33

    defaultConfig {
        applicationId "com.pingidentity.pingone"
        minSdkVersion 26
        targetSdkVersion 33
        versionCode = 16
        versionName "1.7.2"

        // Make sure this is consistent with the redirect URI used in res/raw/auth_config.json,
        // or specify additional redirect URIs in AndroidManifest.xml
        manifestPlaceholders = [
                'appAuthRedirectScheme': 'pingonesdk://sample'
        ]
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "OIDC_ISSUER", OIDC_ISSUER
        buildConfigField "String", "CLIENT_ID", CLIENT_ID
        buildConfigField "String", "SCOPE", SCOPE
        buildConfigField "String", "OIDC_REDIRECT_URI", OIDC_REDIRECT_URI
        /*
         * This is the variable that is injected into build config, while building the application.
         * If you want to use the Android Device Verification feature you should assign a value to
         * this variable in the gradle.properties file. Access this value from the application in the
         * following way:
         * String safetyNetApiKey = BuildConfig.API_KEY;
         */
        buildConfigField("String", "API_KEY", "${safetynet_api_key}")
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

}
dependencies {
    implementation fileTree(dir: 'libs', include: ['*.aar'])
    implementation project(':PingAuthenticationUI')
    implementation 'org.slf4j:slf4j-api:1.7.30'
    implementation 'com.github.tony19:logback-android:2.0.0'

    implementation 'com.madgag.spongycastle:core:1.58.0.0'
    implementation 'com.madgag.spongycastle:bcpkix-jdk15on:1.58.0.0'

    //Import the Firebase BoM (Bill of Materials)
    implementation platform('com.google.firebase:firebase-bom:26.3.0')
    //FireCloud Messaging Services
    implementation 'com.google.firebase:firebase-core'
    implementation 'com.google.firebase:firebase-messaging'

    /*
     * The jose.4.j library is an open source (Apache 2.0) implementation of JWT and the JOSE
     * specification suite
     */
    implementation 'org.bitbucket.b_c:jose4j:0.7.9'
    implementation 'com.google.android.gms:play-services-safetynet:18.0.1'

    //Google's gSon library to build and parse JSON format
    implementation 'com.google.code.gson:gson:2.9.0'

    //to manage lifecycle easily
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.navigation:navigation-fragment:2.5.1'

    /*
     * To easily build cameraX preview
     */
    implementation 'androidx.camera:camera-camera2:1.1.0'
    implementation 'androidx.camera:camera-lifecycle:1.1.0'
    implementation 'androidx.camera:camera-view:1.0.0-alpha31'

    /*
     * ML Kit is a mobile SDK that brings Google's on-device machine learning expertise to a device
     * ML Kit's APIs all run on-device, allowing for real-time use cases where you want to process
     * a live camera stream. This also means that the functionality is available offline.
     */
    implementation 'com.google.mlkit:barcode-scanning:17.0.2'

    /*
     * AppAuth for Android is a client SDK for communicating with OAuth 2.0 and OpenID Connect providers
     * for versions look at https://bintray.com/openid/net.openid/appauth#
     */
    implementation 'net.openid:appauth:0.11.1'

    /*
     * The library that provides ability to use Certificate Transparency
     * https://www.certificate-transparency.org
     */
    implementation("com.appmattus.certificatetransparency:certificatetransparency-android:1.0.0")

    testImplementation 'junit:junit:4.13.2'
}
apply plugin: 'com.google.gms.google-services'
